# the is the name of the exe we want
TARGET=metacc

# this is the source fed to the bison system
BISON_SOURCES+=mcc_parser.yy

# this is the source fed to the flex system
FLEX_SOURCES+=mcc_lexer.l

# we then add this to the other files for editing
OTHER_FILES+=$$BISONSOURCES $$FLEXSOURCES


###########################################################################################
# now we are going to create a Qt compiler ojbect for the EXTRA_COMPILERS FLAG
# this basically has a number of attributes which we need to set as outlined below
###########################################################################################
# this flag list the sources we wish to input to this custom compiler, in this case listed above
# in the BISONSOURCES and FLEXSOURCES variable
bison.input = BISON_SOURCES
flex.input = FLEX_SOURCES

# now we need to say what files to output, for flex we want to create the .cpp
bison.output = ${QMAKE_FILE_BASE}.tab.cc
flex.output = ${QMAKE_FILE_BASE}.cpp

# this is the actual compile command we wish to call in this case it wil be
# bison -o (output).tab.cc input.yy
# flex -o (output).cpp input.l
bison.commands = bison -g -d -o ${QMAKE_FILE_IN_BASE}.tab.cc ${QMAKE_FILE_IN} --verbose
flex.commands = flex -o ${QMAKE_FILE_IN_BASE}.cpp ${QMAKE_FILE_IN}

# once this compiler has been run we need to add the output files to the linker stage in this case
# as we are generating .c files we just add it to the SOURCES variable and it will be compiled
bison.variable_out = SOURCES
flex.variable_out = SOURCES

# this flag ensures that the extra compiler is run before the main c one (as we need to have the .c files
# generated by flex before we use the gcc compiler)
bison.CONFIG += target_predeps
flex.CONFIG += target_predeps

# this tells the make clean command what files to remove
bison.clean = ${QMAKE_FILE_IN_BASE}.tab.*
flex.clean = ${QMAKE_FILE_IN_BASE}.cpp

# this is just the name of the extra compiler used in the make file (make flex for example)
bison.name = bison
flex.name = flex

# finally add this to the qmake project so it will have the makefile generated
QMAKE_EXTRA_COMPILERS += bison
QMAKE_EXTRA_COMPILERS += flex

# we need the fl lib for bison and flex

# tell QtCreator that we using g++ to build metacc.
QMAKE_LINK=g++


# this just cleans out some things for mac
CONFIG += console c++11
CONFIG -= app_bundle

#
TEMPLATE = app
CONFIG -= qt

SOURCES += \
        ast_action.cpp \
        ast_common.cpp \
        ast_describe.cpp \
        ast_glance.cpp \
        main.cpp

HEADERS += \
    ast.h \
    table.h

DISTFILES += \
    calc_mcc1.txt \
    calc_mcc2.txt \
    mcc_lexer.l \
    mcc_parser.yy \
    out_calc_mcc1_l.txt \
    out_calc_mcc1_y.txt \
    syntax.txt

